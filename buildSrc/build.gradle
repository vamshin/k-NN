// Copied from Gradle offline dependency plugin and fixed to work on Gradle 4.10
apply plugin: 'groovy'

apply from: '../build-tools/amazon-sandbox.gradle'
ext {
	offlineRepositoryRoot = rootProject.file("${rootDir.parentFile}/offline-repo").absolutePath
}
repositories {
	if (!sandboxedBuild) {
		mavenCentral()
		maven { url 'https://repo.gradle.org/gradle/libs-releases' }
	}
}

ext {
  mavenVersion = '3.6.0'
  jarjarVersion = '1.2.1'

  thirdpartyLibsDir = "$project.projectDir/libs/"
}

configurations {
  thirdparty
  jarjar
}

dependencies {
	compile gradleApi()
	compile localGroovy()

	compile fileTree(thirdpartyLibsDir)

	// thirdparty dependencies and repackaging
	jarjar "org.gradle.jarjar:jarjar:$jarjarVersion"
	thirdparty "org.apache.maven:maven-model-builder:$mavenVersion"
}

task repackageDependencies(type: Jar) {
	baseName = 'offline-dependencies-plugin-thirdparty-repackaged-all'
	version = project.version
	destinationDir = new File(thirdpartyLibsDir)

	doLast {
		project.ant {
			taskdef name: "jarjar", classname: "com.tonicsystems.jarjar.JarJarTask", classpath: configurations.jarjar.asPath
			jarjar(destfile: archivePath) {
				configurations.thirdparty.each { originalJar ->
					zipfileset(src: originalJar)
				}
				rule(pattern: 'org.**', result: 'io.pry.gradle.offline_dependencies.repackaged.org.@1')
				rule(pattern: 'com.**', result: 'io.pry.gradle.offline_dependencies.repackaged.com.@1')
				rule(pattern: 'licenses.**', result: 'io.pry.gradle.offline_dependencies.repackaged.licenses.@1')
			}
		}
	}
}

if (!sandboxedBuild) {
	compileJava.dependsOn 'repackageDependencies'
}
apply from: '../build-tools/brazil.gradle'
task extractOfflineRepo(type: Copy) {
	onlyIf { file(offline_archive_name).exists() }
	from zipTree(file(offline_archive_name))
	into rootDir.parentFile.parentFile
	doLast { delete file(offline_archive_name) }
}

compileJava.dependsOn extractOfflineRepo